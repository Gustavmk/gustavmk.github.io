---
title: "OpenTofu + CSV"
date: 2025-04-09
toc: false
#comments: true
---

# Guia Pr√°tico: Usando Arquivos CSV no OpenTofu

**Terraform / OpenTF/OpenTofu**: O que ser√° debatido aqui, vou denominar simplesmente como Tofu e ponto. 

> **Nota:** O OpenTofu √© um fork do Terraform, e o nome "OpenTofu" foi escolhido para refletir a natureza aberta e colaborativa do projeto. O OpenTofu √© uma vers√£o do Terraform que √© mantida por uma comunidade de desenvolvedores e usu√°rios, e √© projetada para ser compat√≠vel com o Terraform original, mas com um foco maior em recursos abertos e transpar√™ncia.
>
> [The openTofu Manifesto](https://opentofu.org/manifesto/)


## Introdu√ß√£o 

H√° pessoas que t√™m um amor incontrol√°vel por planilhas. Pois √©‚Ä¶ acabei me envolvendo nesse mundo tamb√©m. Desde que comecei a trabalhar com *infraestrutura como c√≥digo* (IaC), sempre procurei formas de automatizar tarefas repetitivas e agilizar processos, e, durante esse caminho de transi√ßa√µ de controles por fora do c√≥digo, estavam em planilhas, e as op√ß√µes que eu estava trabalhando era muito verboso, mesmo reutilizando c√≥digo n√£o era pr√°tico como ler uma planilha.

Foi a√≠ que, h√° alguns anos, descobri que o [OpenTofu](https://opentofu.org/) permite integrar arquivos CSV diretamente com o Terraform. A ideia √© simples: usar listagens em formato `.csv` (como as que usamos pra inventariar regras de firewall, DNS, ou rotas de rede) e automatizar a cria√ß√£o desses recursos.

Neste guia, vou mostrar como voc√™ pode usar dados de um CSV para criar **rotas em uma Route Table** e **entradas de DNS**, de maneira f√°cil e replic√°vel.

Todos os exemplos est√£o dispon√≠veis no reposit√≥rio **[github.com/drylabs/posts](https://github.com/drylabs/code-examples/tree/main/tf/tofu-plus-csv)**.

No final desse artigo voc√™ aprender√° a consumir CSV usando o Tofu para declarar sua infraestrutura como c√≥digo em seus projetos. 


## Porque CSVs s√£o mais elegantes do que list(maps) tradicionais no Terraform

No Tofu, uma estrutura muito √∫til para modelar dados complexos √© o tipo **list maps** ‚Äî ou seja, uma lista onde cada item √© um mapa (dicion√°rio) com chaves e valores. 

Tradicionalmente, voc√™ poderia definir uma lista de mapas assim:

### Exemplo de uma lista de mapas. 

Vamos entender melhor como trabalhar com isso com o exemplo abaixo.

```hcl
variable "dns_records" {
  type = list(object({
    name  = string
    type  = string
    value = string
  }))

  default = [
    {
      name  = "www"
      type  = "A"
      value = "192.168.0.1"
    },
    {
      name  = "api"
      type  = "CNAME"
      value = "api.example.com"
    }
  ]
}
```
O que acontece nesse exemplo, √© que voc√™ j√° utilizou tr√™s linhas de c√≥digo para definir apenas um registro dentro do list(map). Agora, imagine que voc√™ tenha 100 registros DNS para criar. Voc√™ teria que repetir esse padr√£o 100 vezes, sinigicando n * 3, totalizando 300 linhas. Essa √© uma situa√ß√£o que voc√™ poder√° enfrentar caso n√£o utilize o CSV.

Comma-Separated Values (CSV) √© um formato de arquivo que armazena dados tabulares em texto simples. Ele √© amplamente utilizado para transferir dados entre diferentes sistemas e aplicativos, especialmente em planilhas e bancos de dados.

Essa estrutura √© perfeitamente representada tamb√©m por tabelas e planilhas. Os arquivos no formato CSV, podem ser automaticamente convertidos para esse formato com a fun√ß√£o *`csvdecode()`* que iremos explorar logo abaixo em duas formas de aplicar em seu projeto. 

### Porque isso importa?

- **Simplicidade**: Com o CSV, voc√™ pode ter todos os seus dados em um √∫nico arquivo, facilitando a leitura e a manuten√ß√£o.
- **Flexibilidade**: Voc√™ pode facilmente adicionar, remover ou modificar entradas no CSV sem precisar alterar o c√≥digo do Terraform.
- **D.R.Y**: Don't repeat yourself! Voc√™ pode usar o mesmo arquivo CSV em diferentes m√≥dulos ou projetos, tornando seu c√≥digo mais modular e reutiliz√°vel.

## Exemplo 1 ‚Äì Criando Entradas na Route Table com CSV

Antes de tudo, vamos criar o nosso arquivo CSV chamado `vnet_routes.csv`, com as colunas necess√°rias:

```csv
route_name,address_prefix,next_hop_type,next_hop_ip
route0,1.1.1.1/32,Internet,null
route1,1.1.1.2/32,VirtualNetworkGateway,null
route2,1.1.1.3/32,VnetLocal,null
route4,1.1.1.4/32,None,null
route5,1.1.1.5/32,VirtualAppliance,10.0.0.1
```

> üí°Esse arquivo precisa estar no diret√≥rio raiz do seu m√≥dulo tf

O *Local* ser√° respons√°vel por definir o valor *vnet_routes*, onde podemos relacionar diversas novas vezes a partir dele.
O arquivo CSV precsia ser armazenado a partir do diret√≥rio raiz do modulo em refer√™ncia.

Para transformar a listagem acima no formato CSV. Utilizaremos a fun√ß√£o *`csvdecode()`*. Dessa forma, o Tofu criar√° uma list(map(string)) automaticamente. 

Para fins de demonstra√ß√£o, criei um output para demonstrar o resultado ap√≥s a convers√£o do CSV.

```hcl
locals {
  vnet_routes = csvdecode(file("${path.module}/vnet_routes.csv"))
}

output "vnet_routes" {
  value = local.vnet_routes
}
```
| ![tofu_output](output_locals.png) | 
|:--:| 
| *Exemplo Tofu output de csv para string(map)* |


#### Itera√ß√£o da lista

Ap√≥s a defini√ß√£o dos nossos valores na string(map), faremos o consumo dela no bloco de recurso que declararemos a route table. 

```hcl
resource "azurerm_route" "vnet_routes" {
  for_each            = { for routes in local.vnet_routes : routes.route_name => routes } 

  route_table_name    = azurerm_route_table.main.name
  resource_group_name = azurerm_resource_group.main.name

  name                   = each.value.route_name
  address_prefix         = each.value.address_prefix
  next_hop_type          = each.value.next_hop_type
  
  next_hop_in_ip_address = (each.value.next_hop_type == "VirtualAppliance") == true ? each.value.next_hop_ip : null
}
```

### üîç O que est√° acontecendo?

Para ajudar no entendimento, vamos analisar os principais trechos desse c√≥digo. 

#### Express√£o for_each

`for_each = { for routes in local.vnet_routes : routes.route_name => routes }`

Nessa parte o OpenTofu itera sobre os dados do local.vnet_routes. Desta forma, √© criado um mapa usando route_name como chave. Assim, cada rota ser√° gerenciada de forma independente. Iremos observar esse comportamento mais adiante, ap√≥s a aplica√ß√£o do c√≥digo.

#### Express√£o tern√°ria

`next_hop_in_ip_address = (each.value.next_hop_type == "VirtualAppliance") == true ? each.value.`

  - Se next_hop_type for "VirtualAppliance", o IP de pr√≥ximo salto (next_hop_ip) ser√° usado.
  - Caso contr√°rio, o campo ser√° null.

## Exemplo 2 - Criando entradas de DNS usando locals para definir um valor csv sem ter um arquivo csv no reposit√≥rio

Esse exemplo √© semelhante ao anterior, mas aqui n√£o utilizamos um arquivo CSV. Em vez disso, definimos os valores diretamente no c√≥digo usando `Heredoc Strings`.

```hcl
locals {

  csv_dns_zone_type_cname_drylabs_dev = <<-CSV
    name,type,ttl,records,docs
    dns1,CNAME,3600,drylabs.dev,n/a
    dns2,CNAME,3600,google.com,n/a
    app,CNAME,3600,lb-example.drylabs.dev,n/a
    app2,CNAME,3600,app2.drylabs.dev.cdn.cloudflare.net,n/a
  CSV

  csv_dns_zone_type_a_drylabs_dev = <<-CSV
    name,type,ttl,records,docs
    lb-example,a,3600,1.1.1.1,n/a
  CSV

  dns_zone_type_cname_drylabs_dev = csvdecode(local.csv_dns_zone_type_cname_drylabs_dev)
  dns_zone_type_a_drylabs_dev     = csvdecode(local.csv_dns_zone_type_a_drylabs_dev)
}
```

- üîç Nesse exemplo estamos definindo em locals duas listas separadas. A primeira lista *csv_dns_zone_type_cname_drylabs_dev*, para finalidade de registros como CNAME e a segunda lista *csv_dns_zone_type_a_drylabs_dev* para tipos de registro A. Nesse etapa n√£o est√£o codificadas como csv, por√©m contem todo o conte√∫do necess√°rio para serem consumidas como CSV.
- üîç Ainda em *locals*, os valores *dns_zone_type_cname_drylabs_dev e dns_zone_type_a_drylabs_dev* s√£o definidos como `csvdecode()`, respectivamente. Isso converte as strings em listas de mapas, permitindo o acesso aos dados de forma mais f√°cil.

#### Itera√ß√£o da lista

Nesse momento, repetimos a mesma l√≥gica utilizada no exemplo 1.

```hcl
resource "azurerm_dns_a_record" "main" {
  for_each            = { for k in local.dns_zone_type_a_drylabs_dev : k.name => k }
  zone_name           = azurerm_dns_zone.main.name
  resource_group_name = azurerm_resource_group.main.name

  name    = each.value.name
  records = [each.value.records]
  ttl     = each.value.ttl
}
```

### ‚úÖResultado final 

Para aplicar o c√≥digo acima, foi utilizado o `tofu init, tofu plan -out tfplan e tofu apply "tfplan"`.

Todas as rotas e registros DNS definidos nas planilhas ser√£o criados automaticamente. Reutilizando o msemo trecho de c√≥digo para criar entradas de DNS e rotas, voc√™ pode facilmente adicionar ou remover entradas no CSV sem precisar alterar o c√≥digo do Tofu.

Al√©m disso nesse exemplo n√£o estamos fazendo uso de modulos, possibilitando reduzir ainda mais a complexidade do c√≥digo e reduzindo o numero de repeti√ß√µes de c√≥digo. Entretanto, n√£o √© a finalidade desse post.

| ![apply](tofu-apply.png) | 
|:--:| 
| *Provisionamento dos recursos baseado em listas* |

Chegamos ao final do nosso guia pr√°tico. Agora voc√™ tem uma compreens√£o s√≥lida de como usar arquivos CSV no OpenTofu para criar e gerenciar recursos de forma eficiente.

## üß†Dicas √∫teis e referencias

- ‚úÖPrefira **for_each** ao inv√©s de **count**: O **for_each** funciona melhor que **count** quando os dados s√£o baseados em mapas. Isso possibilita uma f√°cil manuten√ß√£o, pois a remo√ß√£o de uma entrada n√£o afetar√° no ciclo de vida dos demais recursos.
- üß©Campos opcionais - *como o next_hop_ip do primeiro exemplo* - podem ser tratados com tern√°rios.
- üóÉÔ∏èPadronize os cabe√ßalhos do CSV: mantenha os nomes simples e sem espa√ßos para facilitar o seu uso direto nas express√µes each.value.


### üìö Refer√™ncias

- [OpenTofu - csvdecode()](https://opentofu.org/docs/language/functions/csvdecode/)
- [OpenTofu - for_each](https://opentofu.org/docs/language/meta-arguments/for_each/)
- [OpenTofu - Tern√°rio / Conditional Expressions](https://opentofu.org/docs/language/expressions/conditionals/)
- [OpenTofu - Heredoc Strings ](https://opentofu.org/docs/language/expressions/strings/#indented-heredocs)
- [DRY Coding with Terraform](https://jloudon.com/cloud/HashiTalks-ANZ-DRY-Coding-with-Terraform-CSVs-ForEach/)
- 
